// Copyright 06-Apr-2024 ºDeme
// GNU General Public License - V3 <http://www.gnu.org/licenses/>

/// Mobil average.

import "../model";
import "doc/maDoc";

/// Returns a new 'appr' model.
//: [|(model)]
new = \ -> return model.new(
    "MM",
    "Media móvil",
    maDoc.text,
    ["Días", "Banda"],
    [model.dayParam, model.percParam]
  );;

/// Returns a new [. isSold, reference, closes array] of one company,
/// where:
/// state -> is true if the company is not in stock.
/// reference -> is the new company reference.
/// closes -> Closes used for calculating references.
///   Ref   : Reference data of previous calculus
///           ([. state, reference, closes array]).
///           'reference' is equals -1 when it is not fixed the initial one.
///   cl    : Current close.
///   Params: Model parameters.
//: [[. bf[f]]f[f] | [. bf[f]]]
ref = \Ref, cl, Params -> {
  days = Params[0];
  strip = Params[1];
  stripUp = 1.0 + strip;
  stripDown = 1.0 - strip;

  isSold = Ref[0];
  rf = Ref[1];
  Cls = Ref[2];
  if (rf < 0.0) return [. false, cl * stripDown, [0.0, cl, cl]];

  cdays0 = Cls[0];
  cdays = (cdays0 < days) ? cdays0 + 1.0 : cdays0;
  sum = Cls[1];
  :arr HCls = Cls[2:];

  newSum = sum + cl - (cdays >= days ? HCls[0] : 0.0);
  if (cdays >= days) HCls.shift();
  HCls.push(cl);
  avg = newSum / (cdays < days ? cdays + 1.0 : days);

  if (isSold) {
    if (cl > rf) {
      return [. false, avg * stripDown, [cdays, newSum] + HCls];
    } else {
      newRf = avg * stripUp;
      return [. true, newRf < rf ? newRf : rf, [cdays, newSum] + HCls];
    }
  } else {
    if (cl < rf) {
      return [. true, avg * stripUp, [cdays, newSum] + HCls];
    } else {
      newRf = avg * stripDown;
      return [. false, newRf > rf ? newRf : rf, [cdays, newSum] + HCls];
    }
  }
};

//: [ [[f]][f] | [[f]] ]
refs = \:arr Closes, :arr Params -> {
  days = Params[0];
  idays = math.ftoi(days);
  strip = Params[1];
  stripUp = 1.0 + strip;
  stripDown = 1.0 - strip;
  :arr Cls0 = Closes[0];
  ndates = Closes.size();
  ncos = Cls0.size();

  IsSolds = arr.new(false, ncos);
  :arr Refs = arr.map(Closes[0], \cl -> return cl * stripDown;);
  Sums = arr.copy(Closes[0]);

  Refs2 = Refs.copy();
  AllRefs = arr.new(Refs2, ndates);
  for (idate = 1:ndates) {
    :arr ClosesR = Closes.get(idate);
    :arr OldCloses = idate >= idays ? Closes.get(idate - idays) : ClosesR;

    for (ico = 0:ncos) {
      c = ClosesR.get(ico);
      oldC = OldCloses.get(ico);
      rf = Refs.get(ico);
      Sums.set(ico, Sums.get(ico) + c - (idate >= idays ? oldC : 0.0));
      avg = Sums.get(ico) / (idate < idays ? math.itof(idate + 1) : days);

      if (IsSolds.get(ico)) {
        if (c > rf) {
          IsSolds.set(ico, false);
          Refs.set(ico, avg * stripDown);
        } else {
          newRf = avg * stripUp;
          if (newRf < rf) Refs.set(ico, newRf);
          else Refs.set(ico, rf);
        }
      } else {
        if (c < rf) {
          IsSolds.set(ico, true);
          Refs.set(ico, avg * stripUp);
        } else {
          newRf = avg * stripDown;
          if (newRf > rf) Refs.set(ico, newRf);
          else Refs.set(ico, rf);
        }
      }
    }
    AllRefs.set(idate, Refs.copy());
  }
  return AllRefs;
};
