// Copyright 06-Apr-2024 ÂºDeme
// GNU General Public License - V3 <http://www.gnu.org/licenses/>

/// up-up, down-down model.

import "../model";
import "doc/uuddDoc";

/// Returns a new 'appr2' model.
//: [|(model)]
new = \ -> return model.new(
    "SS_BB",
    "Sube-Sube/Baja-Baja",
    uuddDoc.text,
    ["Inicio", "Veloc."],
    [model.percParam, model.percParam]
  );;

/// Returns a new [. isSold, reference, closes array] of one company,
/// where:
/// state -> is true if the company is not in stock.
/// reference -> is the new company reference.
/// closes -> Closes used for calculating references.
///   Ref   : Reference data of previous calculus
///           ([. state, reference, closes array]).
///           'reference' is equals -1 when it is not fixed the initial one.
///   cl    : Current close.
///   Params: Model parameters.
//: [[. bf[f]]f[f] | [. bf[f]]]
ref = \Ref, cl, Params -> {
  start = Params[0];
  upStart = 1.0 + start;
  downStart = 1.0 - start;
  speed = Params[1] * 10.0;

  isSold = Ref[0];
  rf = Ref[1];
  Cls = Ref[2];
  if (rf < 0.0) return [. false, cl * downStart, [cl]];

  prvC = Cls[0];
  if (isSold) {
    if (cl > rf) {
      return [. false, cl * downStart, [cl]];
    } else {
      dif = cl - prvC;
      rf0 = rf * (prvC + dif * speed) / prvC;
      rf2 = rf < rf0 ? rf : rf0;
      return [. true, rf2, [cl]];
    }
  } else {
    if (cl < rf) {
      return [. true, cl * upStart, [cl]];
    } else {
      dif = cl - prvC;
      rf0 = rf * (prvC + dif * speed) / prvC;
      rf2 = rf > rf0 ? rf : rf0;
      return [. false, rf2, [cl]];
    }
  }
};

//: [ [[f]][f] | [[f]] ]
refs = \:arr Closes, :arr Params -> {
  start = Params[0];
  upStart = 1.0 + start;
  downStart = 1.0 - start;
  speed = Params[1] * 10.0;
  :arr Cls0 = Closes[0];
  ndates = Closes.size();
  ncos = Cls0.size();

  IsSolds = arr.new(false, ncos);
  Refs = []/f/;
  for (cl = Cls0) Refs.push(cl * downStart);

  Refs2 = Refs.copy();
  AllRefs = arr.new(Refs2, ndates);
  for (idate = 1:ndates) {
    :arr ClosesR = Closes.get(idate);
    :arr PrvCloses = Closes.get(idate - 1);

    for (ico = 0:ncos) {
      c = ClosesR.get(ico);
      prvC = PrvCloses.get(ico);
      rf = Refs.get(ico);

      if (IsSolds.get(ico)) {
        if (c > rf) {
          IsSolds.set(ico, false);
          Refs.set(ico, c * downStart);
        } else {
          dif = c - prvC;
          rf0 = rf * (prvC + dif * speed) / prvC;
          rf2 = rf < rf0 ? rf : rf0;
          Refs.set(ico, rf2);
        }
      } else {
        if (c < rf) {
          IsSolds.set(ico, true);
          Refs.set(ico, c * upStart);
        } else {
          dif = c - prvC;
          rf0 = rf * (prvC + dif * speed) / prvC;
          rf2 = rf > rf0 ? rf : rf0;
          Refs.set(ico, rf2);
        }
      }
    }
    AllRefs.set(idate, Refs.copy());
  }
  return AllRefs;
};
