// Copyright 15-Apr-2025 ÂºDeme
// GNU General Public License - V3 <http://www.gnu.org/licenses/>

/// Data base management.

import "db/confTb";
import "db/pathsTb";
import "cts";
_
klen = 20;
_
//: [|s]
dbStateKeyTb = \ -> return file.cat(cts.dataPath(), ["dbStateKey.tb"]);;

/// Initializes data base
//: [|]
init = \ -> {
  if (!file.exists(cts.dataPath())) {
    file.mkdir(cts.dataPath());
    file.write(file.cat(cts.dataPath(), ["version.tb"]), cts.dataVersion);
    file.write(dbStateKeyTb(), cryp.genK(klen));

    confTb.init();
    pathsTb.init();
    return;
  }
  throw "Data base already is initialized";
};

/// Returns the state identifier.
//: [|s]
readKey = \ -> return str.trim(file.read(dbStateKeyTb()));;

/// Checks the state identifier 'key' and returns:
///   - If 'key' is valid, a new identifier.
///   - If 'key' is not valid, an empty string.
//: [s|s]
checkKey = \key -> {
  if (str.trim(file.read(dbStateKeyTb())) == key) {
    newKey = cryp.genK(klen);
    file.write(dbStateKeyTb(), newKey);
    return newKey;
  }
  return "";
};

