// Copyright 12-May-2025 ÂºDeme
// GNU General Public License - V3 <http://www.gnu.org/licenses/>

/// Portfolio operations.
/// A porfolio is a dictionary {nick -> [stocks-price]}.

/// Adds a new entry in place.
/// If there are more stocks, new stocks are added and average price (wihout
/// rounding) is calculated.
///   portfolio: Stocks portfolio.
///   nick     : Company nick.
///   stocks   : Stocks to add.
///   price    : Price of each stock.
//: [{[. if]}sif|]
add = \:dic portfolio, nick, stocks, price -> {
  EOp = portfolio.get(nick);
  if (!!EOp) {
    E = EOp!;
    ss = E[0] + stocks;
    pr = (math.itof(E[0]) * E[1] + math.itof(stocks) * price) / math.itof(ss);
    portfolio.put(nick, [. ss, pr]);
    return;
  }

  portfolio.put(nick, [. stocks, price]);
};

/// Removes stocks of a company and return a tuple [price-error].
/// Price (without rounding) if there are stocks, otherwise returns 0.0.
/// Error is "" if there are stocks enough to make the operation. Otherwise
/// returns a message.
///   portfolio: Stocks portfolio.
///   nick     : Company nick.
///   stocks   : Stocks to add.
//: [{[. if]}si|[. fs]]
remove = \:dic portfolio, nick, stocks -> {
  EOp = portfolio.get(nick);
  if (!EOp) return [. 0.0, nick + " has not stocks"];
  E = EOp!;
  if (E[0] < stocks) {
    portfolio.remove(nick);
    return [.
        E[1],
        str.fmt(
          "[%v] Try to remove %v stocks with only there are %v",
          [. nick, stocks, E[0]]
        )
      ];
  }

  if (E[0] > stocks) E[0] -= stocks;
  else portfolio.remove(nick);

  return [. E[1], ""];
};

