// Copyright 21-May-2025 ÂºDeme
// GNU General Public License - V3 <http://www.gnu.org/licenses/>

/// Trading page.

import "libdm/svrp";
import "libdm/log";
import "data/acc/ann";
import "data/acc/settlement";
import "data/acc/pf";
import "data/invOperation";
import "db/acc/diariesDb";
import "db/trading/closesTb";
import "db/trading/refsTb";
import "db/invOperationsTb";
import "db";
import "cts";
import "fns";
import "qspr";

//: [{s}|s]
process = \Rq -> {
  rq = js.rs(Rq\rq);
  switch (rq) {

    "idata": {
      okV = [true];
      :arr InvOperations = invOperationsTb.read(); // [<invOperation>.]
      Rebuys = {}/s/; // {nick -> date}

      :arr Ys = diariesDb.investorYears();
      for (y = Ys[:2]) ann.rebuys(Rebuys, diariesDb.investorAnns(y));
      :arr Anns = !Ys ? []/(ann)/ : diariesDb.investorAnns(Ys[0]);
      :settlement set = ann.mkSettlement(Anns);
      if (!!set.Errors) {
        okV! = false;
        for (e = set.Errors) log.error(e);
      }

      Ponds = fns.getIbexPonderations(true);
      IbexCos = {}/i/;
      for (k, v = Ponds) IbexCos.put(k, math.ftoi(v * cts.ibexInvestment));

      return svrp.mk({
        ok: js.wb(okV!),
        Portfolio: dic.toJs(set.Portfolio, \V -> return js.wa([
            js.wi(V[0]),
            js.wf(V[1])
          ]);),
        InvOperations:  InvOperations.toJs(invOperation.toJs),
        Closes: dic.toJs(closesTb.readOnlyCloses(), js.wf), // {nick -> close}
        Rebuys: Rebuys.toJs(js.ws),
        IbexCos: IbexCos.toJs(js.wi),
        dbKey: js.ws(db.readKey())
      });
    }
    "update": {
      qspr.updateOperations();
      return svrp.mk({ dbKey: js.ws(db.readKey()) });
    }
    default: throw "Value of rq (" + rq + ") is not valid";
  }
};
