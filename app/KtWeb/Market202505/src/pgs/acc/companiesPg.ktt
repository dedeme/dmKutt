// Copyright 23-May-2025 ÂºDeme
// GNU General Public License - V3 <http://www.gnu.org/licenses/>

/// Companies page.

import "libdm/svrp";
import "libmkt/quotes";
import "libmkt/models";
import "libmkt/model";
import "db/cosTb";
import "db/svCodesTb";
import "db/quotesDb";
import "db/investorTb";
import "db/initRefsTb";
import "db/acc/diariesDb";
import "db/trading/closesTb";
import "data/initRef";
import "data/acc/ann";
import "data/acc/settlement";
import "data/co/cos";
import "data/co/co";
import "data/investor/investor";
import "data/investor/istrategy";
import "fns";

_
//: [s|s]
mkUrl = \nick -> {
  Codes = svCodesTb.read("INVES");
  Code = dic.get(Codes, nick);
  if (!!Code)
    return str.fmt(
      "https://es.investing.com/equities/%v", [. Code!]
    );
  return "";
};

_
//: [s|s]
rpErr = \error -> return svrp.mk({error: js.ws(error)});;

//: [{s}|s]
process = \Rq -> {
  rq = js.rs(Rq\rq);
  switch (rq) {
    "idata": {
      // [<ann>.]
      :arr Anns = diariesDb.investorAnns(diariesDb.investorYears()[0]);
      :settlement set = ann.mkSettlement(Anns);
      Pf = set.Portfolio;
      Lst = []/[. sbbs]/;
      for (:co c = cosTb.read()[cos.Cos]) {
        nk = c.nick;
        Lst.push([. nk, c.isSelected, !!dic.get(Pf, nk), mkUrl(nk)]);
      }

      return svrp.mk({
        Lst: arr.toJs(Lst, \E -> return js.wa([
            js.ws(E[0]),
            js.wb(E[1]),
            js.wb(E[2]),
            js.ws(E[3])
          ]);)
      });
    }
    "nickData": {
      nk = js.rs(Rq\nick);
      strgOp = dic.get(investorTb.read()[investor.Nicks], nk);
      if (!strgOp) return rpErr(str.fmt("Strategy of %v not found", [. nk]));

      :istrategy strg = strgOp!;
      mdOp = models.get(strg.modelId);
      if (!mdOp) return rpErr("Model " + strg.modelId + " not found");

      :model md = mdOp!;
      Params = strg.Params;

      :quotes qts = fns.getOpenCloses(nk);
      cl = arr.peek(qts.Closes)[0];
      :arr Refs = fns.getReferences(nk, md.id, Params, qts, false);

      :arr Anns = diariesDb.investorAnns(diariesDb.investorYears()[0]);
      :settlement set = ann.mkSettlement(Anns);
      Pf = set.Portfolio;
      eOp = dic.get(Pf, nk);
      stocks = !eOp ? 0 : eOp![0];
      price = !eOp ? -1.0 : eOp![1];

      return svrp.mk({
        error: js.ws(""),
        price: js.wf(price),
        profits: js.wf(math.itof(stocks) * (cl - price)),
        Dates: arr.toJs(qts.Dates, js.ws),
        Closes: arr.toJs(qts.Closes, \Cs -> return js.wf(Cs[0]);),
        Refs: Refs.toJs(js.wf)
      });
    }
    default: throw "Value of rq (" + rq + ") is not valid";
  }
};
