// Copyright 27-Jul-2025 ÂºDeme
// GNU General Public License - V3 <http://www.gnu.org/licenses/>

/// Data base

import "libdm/log";
import "data/cardId";
import "cts";

// Returns path of a period.
//    period: One of cts.periods.
_
//: [s|s]
periodPath = \pr -> return file.cat(cts.dataPath(), ["results", pr]);;

_
//: [|s]
cardsTbPath = \ -> return file.cat(cts.dataPath(), ["cards.tb"]);;

/// Initializes program.
/// Must be called the first time that the program is called.
//: [|]
init = \ -> {
  p = cts.dataPath();
  file.mkdir(p);
  for (pr = cts.periods()) file.mkdir(periodPath(pr));
  writeCards([]);
  log.reset();
};

/// Returns dates from before to after of a period.
///   period: One of cts.periods.
//: [s|[s]]
periodDates = \period -> {
  :arr Ds = file.dir(periodPath(period));
  Ds.sort(\d1, d2 -> return d1 < d2;);
  return Ds;
};

/// Reads data of a period (invTable JSONized).
///   period: One of cts.periods.
///   date  : Date to read. If it is not found an exception is raised.
//: [ss|s]
read = \period, date ->
  return file.read(file.cat(periodPath(period), [date]));;

/// Reads data (invTable JSONized) of the last period available.
//: [|s]
readLast = \ -> {
  :arr Ds = periodDates(cts.daily);
  return file.read(file.cat(periodPath(cts.daily), [Ds.peek()]));
};

/// Writes data of a period.
///   period: One of cts.periods.
///   date  : Date to read. If it is not found an exception is raised.
///   table : An invTable JSONized.
//: [sss|]
write = \period, date, table ->
  file.write(file.cat(periodPath(period), [date]), table);;

/// Removes old data of periods.
//: [|]
clean = \ -> {
  for (p = cts.periods()) {
    :arr Dts = periodDates(p);
    ExtraDates = Dts.take(Dts.size() - cts.datesInPeriod);
    for (d = ExtraDates)
      file.del(file.cat(periodPath(p), [d]));
  }
};

/// Reads investors cards JSONized.
//: [|s]
readCardsJs = \ -> return file.read(cardsTbPath());;

/// Reads investors cards.
//: [|[(cardId)]]
readCards = \ -> return arr.fromJs(readCardsJs(), cardId.fromJs);;

/// Writes investors cards.
//: [[(cardId)]|]
writeCards = \Cds -> file.write(cardsTbPath(), arr.toJs(Cds, cardId.toJs));;
