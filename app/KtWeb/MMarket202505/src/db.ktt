// Copyright 05-May-2025 ÂºDeme
// GNU General Public License - V3 <http://www.gnu.org/licenses/>

/// Data base management.

import "libmkt/models";
import "libmkt/model";
import "libmkt/quotes";
import "extDb/quotesDb";
import "data/evEs";
import "data/evE";
import "updater";
import "cts";
//import "fns";

/// Initializes data base
//: [|]
init = \ -> {
  dataPath = cts.dataPath();
  if (!file.exists(dataPath)) {
    file.mkdir(dataPath);
    file.write(file.cat(dataPath, ["version.tb"]), cts.dataVersion);

    for (p = cts.periods()) file.mkdir(file.cat(dataPath, [p]));
    updater.run();

    return;
  }
  throw "Data base already is initialized";
};

/// Returns dates of a period, from before to after.
//: [s|[s]]
dates = \ period -> {
  :arr Ls = file.dir(file.cat(cts.dataPath(), [period]));
  Ls.sort(\d1, d2 -> return d1 < d2;);
  return Ls;
};

/// Returns unsorted model identifiers of the last date.
//: [|[s]]
modelIds = \ -> return file.dir(
    file.cat(cts.dataPath(), ["daily", arr.peek(dates("daily"))])
  );;

/// Writes evaluation table.
///   date : Date to write.
///   mdId : Model identifier.
///   evals: Evaluation array.
//: [ss[(evE)]|]
write = \date, mdId, :arr Evals -> {
  //: [s|i]
  weekInYear = \d -> {
    :time tm = time.fromStr(d)!;
    day1 = time.newDate(1, 1, tm.year());
    wdEn = time.weekday(day1);
    wd = wdEn == 0 ? 6 : wdEn - 1;
    dfDays = tm.dfDays(day1);
    return (dfDays + wd) / 7;
  };

  dataPath = cts.dataPath();
  :arr dailyDates = dates("daily");
  dailyDir = file.cat(dataPath, ["daily", date]);
  file.mkdir(dailyDir);
  file.write(file.cat(dailyDir, [mdId]), Evals.toJs(evE.toJs));
  for (d = dailyDates.take(dailyDates.size() - cts.datesInPeriod))
    file.del(file.cat(dataPath, ["daily", d]));

  :arr weeklyDates = dates("weekly");
  :file weeklyDir = file.cat(dataPath, ["weekly", date]);
  if (
    !weeklyDates |
    weeklyDir.exists() |
    weekInYear(date) != weekInYear(weeklyDates.peek())
  ) {
    file.mkdir(weeklyDir);
    file.write(file.cat(weeklyDir, [mdId]), Evals.toJs(evE.toJs));
    for (d = weeklyDates.take(weeklyDates.size() - cts.datesInPeriod))
      file.del(file.cat(dataPath, ["weekly", d]));
  }

  :arr monthlyDates = dates("monthly");
  :file monthlyDir = file.cat(dataPath, ["monthly", date]);
  if (
    !monthlyDates |
    monthlyDir.exists() |
    date[:6] != monthlyDates.peek()[:6]
  ) {
    file.mkdir(monthlyDir);
    file.write(file.cat(monthlyDir, [mdId]), Evals.toJs(evE.toJs));
    for (d = monthlyDates.take(monthlyDates.size() - cts.datesInPeriod))
      file.del(file.cat(dataPath, ["monthly", d]));
  }
};

/// Reads evaluation table.
/// If the table does not exist returns an empty array.
///   period: Data period.
///   date  : Date to write.
///   mdId  : Model identifier.
//: [sss|[(evE)]]
read = \period, date, mdId -> {
  p = file.cat(cts.dataPath(), [period, date, mdId]);
  return file.exists(p)
    ? arr.fromJs(file.read(p), evE.fromJs)
    : []/(evE)/
  ;
};

